description = "Ego vehicle makes a right turn at an intersection and must yield when pedestrian crosses the crosswalk."
param map = localPath('../../maps/Town05.xodr')
param carla_map = 'Town05'
model scenic.simulators.carla.model
MODEL = 'vehicle.lincoln.mkz_2017'

intersection = Uniform(*filter(lambda i: i.is4Way or i.is3Way, network.intersections))

egoManeuver = Uniform(*filter(lambda m: m.type is ManeuverType.RIGHT_TURN, intersection.maneuvers))
egoInitLane = egoManeuver.startLane
egoTrajectory = [egoInitLane, egoManeuver.connectingLane, egoManeuver.endLane]
egoSpawnPt = new OrientedPoint in egoInitLane.centerline

tempSpawnPt = egoInitLane.centerline[-1]

param EGO_SPEED = Range(7, 10)
EGO_BRAKE = 1.0
param SAFETY_DIST = Range(10, 15)
CRASH_DIST = 5

behavior EgoBehavior(trajectory):
	flag = True
	try:
		do FollowTrajectoryBehavior(target_speed=globalParameters.EGO_SPEED, trajectory=trajectory)
	interrupt when withinDistanceToAnyObjs(self, globalParameters.SAFETY_DIST) and (ped in network.drivableRegion) and flag:
		flag = False
		while withinDistanceToAnyObjs(self, globalParameters.SAFETY_DIST + 3):
			take SetBrakeAction(EGO_BRAKE)
	interrupt when withinDistanceToAnyObjs(self, CRASH_DIST):
		terminate

ego = new Car at egoSpawnPt,
	with rolename 'hero',
	with blueprint MODEL,
	with behavior EgoBehavior(egoTrajectory)

PED_MIN_SPEED = 1.0
PED_THRESHOLD = 20

behavior PedestrianBehavior():
	do CrossingBehavior(ego, PED_MIN_SPEED, PED_THRESHOLD)

ped = new Pedestrian right of tempSpawnPt by 5,
	facing ego.heading,
	with regionContainedIn None,
	with behavior PedestrianBehavior()

EGO_INIT_DIST = [20, 25]
TERM_DIST = 50

require EGO_INIT_DIST[0] <= (distance to intersection) <= EGO_INIT_DIST[1]
terminate when (distance to egoSpawnPt) > TERM_DIST

param weather = 'ClearNoon'
